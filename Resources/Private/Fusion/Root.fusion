include: Override.fusion

prototype(Psmb.Ajaxify:RenderPath) {
	@class = 'Psmb\\Ajaxify\\Fusion\\RenderPathImplementation'
	# every path resolution is relative to the Ajaxify prototype, so if you
	# overwrite it, make sure to adjust this property
	prototypeName = 'Psmb.Ajaxify:Ajaxify'
	entryIdentifier = Neos.Fusion:Join {
		key = ${key}
		@process.hash {
			expression = ${String.sha1(value)}
			@position = 'end'
		}
	}
}

prototype(Psmb.Ajaxify:Loader) < prototype(Neos.Fusion:Value) {
	value = ${'<div class="spinner"><div class="bounce1"></div><div class="bounce2"></div><div class="bounce3"></div></div>'}
}

# Use this object as a processor on any path
prototype(Psmb.Ajaxify:Ajaxify) < prototype(Neos.Fusion:Component) {
	# The processor is disabled in BE or when rendering the AJAX request
	@if.disableProcessor = ${!request.arguments.ajaxPathKey && !documentNode.context.inBackend}

	entryIdentifier = Neos.Fusion:DataStructure

	renderer = Neos.Fusion:Tag {
		tagName = 'a'
		attributes.data-ajaxify = ${true}
		attributes.href = Neos.Neos:NodeUri {
			node = ${documentNode}
			additionalParams {
				# The argument "node" is already used by the uri builder
				contentNode = ${node.contextPath}
				ajaxPathKey = Psmb.Ajaxify:RenderPath {
					entryIdentifier.@apply.props = ${props.entryIdentifier}
					context = ${props.context}
				}
			}
		}
		content = Psmb.Ajaxify:Loader
	}
}

prototype(Psmb.Ajaxify:Renderer) {
	@class = 'Psmb\\Ajaxify\\Fusion\\RendererImplementation'
	# The argument "node" is already used
	# We receive a context-path and need to find the respective node
	contentNode = ${String.split(request.arguments.contentNode || '', '@')[0]}
	contentNode.@process.convertToNode = ${q(documentNode).find(value).get(0) || documentNode}
	node = ${this.contentNode}
	pathKey = ${request.arguments.ajaxPathKey}
	@cache {
		mode = 'uncached'
		context {
			1 = 'documentNode'
			2 = 'node'
		}
	}
}

# Prevents search engines from indexing the partly rendered document content
prototype(Psmb.Ajaxify:UnindexedResponse) < prototype(Neos.Fusion:Http.Message) {
	httpResponseHead.headers {
		X-Robots-Tag = 'noindex, follow'
	}
	content = Psmb.Ajaxify:Renderer
}

root.ajaxify {
	@position = 'start'
	condition = ${request.arguments.ajaxPathKey}
	renderer = Psmb.Ajaxify:UnindexedResponse
}

prototype(Psmb.Ajaxify:JsTag) < prototype(Neos.Fusion:Tag) {
	tagName = 'script'
	attributes.src = Neos.Fusion:ResourceUri {
		path = 'resource://Psmb.Ajaxify/Public/ajax.js'
	}
}
prototype(Psmb.Ajaxify:CssTag) < prototype(Neos.Fusion:Tag) {
	tagName = 'link'
	attributes.rel = 'stylesheet'
	attributes.href = Neos.Fusion:ResourceUri {
		path = 'resource://Psmb.Ajaxify/Public/loader.css'
	}
}
